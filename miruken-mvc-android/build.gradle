apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-kapt'

version = packageVersion

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "com.miruken:miruken:$mirukenVersion"
    api "com.miruken:miruken-mvc:$mirukenVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

def artifactName = 'miruken-mvc-android'

publishing {
    publications {
        maven(MavenPublication) {
            from components.android
            groupId    mirukenGroupId
            artifactId artifactName
            pom.withXml {
                def properties = asNode().appendNode('properties')
                properties.appendNode('maven.compiler.source', '1.8')
                properties.appendNode('maven.compiler.target', '1.8')
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key  = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
    pkg {
        name     = artifactName
        userOrg  = mirukenOrg
        repo     = mirukenRepo
        licenses = [mirukenLicense]
        vcsUrl   = mirukenVcsUrl
        version {
            released = new Date()
        }
    }
}
